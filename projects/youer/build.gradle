import net.neoforged.jarcompatibilitychecker.gradle.CompatibilityTask
import net.neoforged.jarcompatibilitychecker.gradle.JCCPlugin
import net.neoforged.jarcompatibilitychecker.gradle.ProvideNeoForgeJarTask
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.jarcompatibilitychecker' version '0.1.12'
    id 'net.neoforged.gradleutils'
    id 'neoforge.versioning'
    id 'de.undercouch.download' version '5.6.0'
}

apply plugin: net.neoforged.neodev.NeoDevPlugin

// Because of the source set reference.
evaluationDependsOn(":neoforge-coremods")

gradleutils.setupSigning(project: project, signAllPublications: true)

sourceSets {
    main {
        java {
            srcDirs rootProject.file('src/main/java')
        }
        resources {
            srcDirs rootProject.file('src/main/resources'), rootProject.file('src/generated/resources')
        }
    }
}

final checkVersion = JCCPlugin.providePreviousVersion(
        project.providers, project.providers.provider({ ['https://maven.neoforged.net/releases'] }), project.providers.provider({ 'net.neoforged:neoforge' }),
        project.provider { project.version }.map { ver -> CompatibilityTask.VersionComponentTest.MINOR.predicate(ver) }
)
final createCompatJar = tasks.register('createCompatibilityCheckJar', ProvideNeoForgeJarTask) {
    // Use the same jar that the patches were generated against
    cleanJar.set(tasks.generateClientBinPatches.cleanJar)
    maven.set('https://maven.neoforged.net/releases')
    artifact.set('net.neoforged:neoforge')
    version.set(checkVersion)
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(java_version)
    }
}
checkJarCompatibility {
    isAPI = true
    baseJar = createCompatJar.flatMap { it.output }
}

neoDev {
    mods {
        minecraft {
            sourceSet sourceSets.main
        }
        "neoforge-coremods" {
            sourceSet project(":neoforge-coremods").sourceSets.main
        }
    }
}

dependencies {

    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    compileOnly 'org.projectlombok:lombok:1.18.30'

    // For an overview of what the nonstandard configurations do,
    // have a look at NeoDevConfigurations.java in the buildSrc folder.

    neoFormData("net.neoforged:neoform:${project.minecraft_version}-${project.neoform_version}") {
        capabilities {
            requireCapability 'net.neoforged:neoform'
        }
        endorseStrictVersions()
    }
    neoFormDependencies("net.neoforged:neoform:${project.minecraft_version}-${project.neoform_version}") {
        capabilities {
            requireCapability 'net.neoforged:neoform-dependencies'
        }
        endorseStrictVersions()
    }

    moduleLibraries "cpw.mods:securejarhandler:${project.securejarhandler_version}"
    for (var asmModule : ["org.ow2.asm:asm", "org.ow2.asm:asm-commons", "org.ow2.asm:asm-tree", "org.ow2.asm:asm-util", "org.ow2.asm:asm-analysis"]) {
        moduleLibraries(asmModule) {
            // Vanilla ships with ASM 9.3 transitively (via their OpenID connect library dependency), we require
            // ASM in a more recent version and have to strictly require this to override the strict Minecraft version.
            version {
                strictly project.asm_version
            }
        }
    }
    moduleLibraries "cpw.mods:bootstraplauncher:${project.bootstraplauncher_version}"
    moduleLibraries "net.neoforged:JarJarFileSystems:${project.jarjar_version}"

    libraries("com.mohistmc.fancymodloader:loader:${project.fancy_mod_loader_version}") {
        exclude group: 'org.slf4j'
        exclude group: 'net.fabricmc'
    }
    libraries("com.mohistmc.fancymodloader:earlydisplay:${project.fancy_mod_loader_version}") {
        exclude group: 'org.lwjgl'
        exclude group: 'org.slf4j'
        exclude group: 'net.fabricmc'
    }
    libraries "net.neoforged:accesstransformers:${project.accesstransformers_version}"
    libraries "net.neoforged:bus:${project.eventbus_version}"
    libraries "net.neoforged:coremods:${project.coremods_version}"
    libraries "cpw.mods:modlauncher:${project.modlauncher_version}"
    libraries "net.neoforged:mergetool:${project.mergetool_version}-api"
    libraries "com.electronwill.night-config:core:${project.nightconfig_version}"
    libraries "com.electronwill.night-config:toml:${project.nightconfig_version}"
    libraries "org.apache.maven:maven-artifact:${project.apache_maven_artifact_version}"
    libraries "net.jodah:typetools:${project.typetools_version}"
    libraries("net.fabricmc:sponge-mixin:${project.mixin_version}") { transitive = false }
    libraries "org.openjdk.nashorn:nashorn-core:${project.nashorn_core_version}"
    libraries("net.neoforged:JarJarSelector:${project.jarjar_version}") {
        exclude group: 'org.slf4j'
    }
    // We depend on apache commons directly as there is a difference between the version the server uses and the one the client does
    libraries "org.apache.commons:commons-lang3:${project.apache_commons_lang3_version}"
    libraries("net.neoforged:JarJarMetadata:${project.jarjar_version}") {
        exclude group: 'org.slf4j'
    }

    compileOnly "org.jetbrains:annotations:${project.jetbrains_annotations_version}"

    userdevCompileOnly jarJar("io.github.llamalad7:mixinextras-neoforge:${project.mixin_extras_version}")

    // Must be implementation instead of compileOnly so that running dependent projects such as tests will trigger (re)compilation of coremods.
    // (Only needed when compiling through IntelliJ non-delegated builds - otherwise `compileOnly` would work).
    implementation(jarJar(project(":neoforge-coremods")))

    // craftbukkit
    libraries 'org.fusesource.jansi:jansi:2.4.2'
    libraries 'com.googlecode.json-simple:json-simple:1.1.1'
    libraries 'org.xerial:sqlite-jdbc:3.46.0.0'
    libraries 'com.mysql:mysql-connector-j:8.4.0'
    libraries('net.md-5:SpecialSource:1.11.2') {
        exclude group: 'com.opencsv'
    }
    libraries 'org.apache.logging.log4j:log4j-iostreams:2.22.1'
    // spigot
    libraries 'commons-codec:commons-codec:1.16.0'
    libraries 'net.sf.jopt-simple:jopt-simple:5.0.4'

    libraries 'io.izzel:tools:1.3.0'
    libraries 'com.mohistmc:dynamicenum:0.3.1'
    libraries 'com.mohistmc:i18n:0.6'
    libraries 'com.mohistmc:json:0.5'
    libraries 'com.mohistmc:tools:0.6.2'
    // NOTE: using librariesvault 1.21.1-1 because 1.21-1 is not yet available on public Maven; this does not affect the Minecraft target version.
    libraries ('com.mohistmc:librariesvault:1.21.1-1') {
        exclude group: 'org.yaml'
        exclude group: 'io.leangen.geantyref'
        exclude group: 'org.spongepowered'
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'org.apache.commons'
    }
    libraries 'com.konghq:unirest-java-core:4.5.1'
    libraries 'org.mariadb.jdbc:mariadb-java-client:3.1.4'
    implementation(jarJar('commons-lang:commons-lang:2.6-mohist'))
    libraries "net.minecrell:terminalconsoleappender:1.4.0"
    libraries "org.jline:jline-reader:3.30.6"   // Dep of TerminalConsoleAppender
    libraries "org.jline:jline-terminal:3.30.6" // Dep of TerminalConsoleAppender
    libraries "org.jline:jline-terminal-jansi:3.30.6" // Dep of TerminalConsoleAppender

    // Paper start
    libraries "org.jspecify:jspecify:1.0.0"
    libraries 'net.md-5:bungeecord-chat:1.20-R0.2-deprecated+build.18'
    libraries "net.neoforged:srgutils:1.0.0"
    libraries("net.kyori:adventure-api:4.17.0")
    libraries("net.kyori:adventure-key:4.17.0")
    libraries("net.kyori:adventure-text-logger-slf4j:4.17.0")
    libraries("net.kyori:adventure-text-serializer-ansi:4.17.0")
    libraries("net.kyori:adventure-text-minimessage:4.17.0")
    libraries("net.kyori:adventure-text-serializer-gson:4.17.0")
    libraries("net.kyori:adventure-text-serializer-json:4.17.0")
    libraries("net.kyori:adventure-text-serializer-legacy:4.17.0")
    libraries("net.kyori:adventure-text-serializer-plain:4.17.0")
    libraries("net.kyori:ansi:1.0.3")
    libraries("net.kyori:examination-api:1.3.0")
    libraries("net.kyori:examination-string:1.3.0")
    libraries("net.kyori:option:1.0.0")
    libraries("com.mohistmc:AutoRenamingTool:2.0.3") // Paper - remap plugins
    libraries("io.netty:netty-codec-haproxy:4.1.97.Final") // Paper - Add support for proxy protocol
    libraries("com.velocitypowered:velocity-native:3.3.0-SNAPSHOT") {
        exclude group: 'io.netty'
    }
    // Paper end

    // LibraryLoader start
    libraries 'org.checkerframework:checker-qual:3.33.0'
    libraries 'org.eclipse.sisu:org.eclipse.sisu.inject:0.9.0.M2'
    libraries 'org.codehaus.plexus:plexus-interpolation:1.26'
    libraries 'org.codehaus.plexus:plexus-utils:3.5.1'
    libraries 'com.google.protobuf:protobuf-java:3.25.1'
    libraries('org.dom4j:dom4j:2.1.4') {
        exclude group: 'javax.xml.bind'
        exclude group: 'javax.xml.stream'
        exclude group: 'jaxen'
        exclude group: 'xpp3'
    }
    // LibraryLoader end

    // Pufferfish start
    libraries 'io.sentry:sentry:5.4.0'
    // libraries 'org.mozilla:rhino-engine:1.7.14'
    libraries 'dev.omega24:upnp4j:1.0'
    libraries ("com.github.carleslc.Simple-YAML:Simple-Yaml:1.8.4") { // Purpur
        exclude group: 'org.yaml'
    }
    // Pufferfish end

    compileOnly("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") { transitive = false }
    compileOnly("net.createmod.ponder:Ponder-NeoForge-${minecraft_version}:${ponder_version}")
}

neoDev {
    runs {
        configureEach {
            gameDirectory = layout.projectDir.dir("run/$name")
            systemProperty("terminal.ansi", "true")
        }
        client {
            client()
        }

        server {
            server()
        }

        gameTestServer {
            type = "gameTestServer"
        }
        data {
            data()
            programArguments.addAll '--mod', 'neoforge', '--flat', '--all', '--validate',
                    '--existing', rootProject.file("src/main/resources").absolutePath,
                    '--output', rootProject.file("src/generated/resources").absolutePath
        }
    }
}

tasks.withType(JavaCompile.class).configureEach {
    // Increase memory used during compilation, to avoid OutOfMemoryErrors
    options.forkOptions.memoryMaximumSize = '2g'
}

tasks.withType(Javadoc.class).configureEach {
    options.tags = [
            'apiNote:a:<em>API Note:</em>',
            'implSpec:a:<em>Implementation Requirements:</em>',
            'implNote:a:<em>Implementation Note:</em>'
    ]
    options.addStringOption('Xdoclint:all,-missing', '-public')
}

AdhocComponentWithVariants javaComponent = (AdhocComponentWithVariants) project.components.findByName("java")
// Ensure the two default variants are not published, since they
// contain Minecraft classes
javaComponent.withVariantsFromConfiguration(configurations.apiElements) {
    it.skip()
}
javaComponent.withVariantsFromConfiguration(configurations.runtimeElements) {
    it.skip()
}

// Resolvable configurations only
configurations {
    runtimeElements {
        attributes {
            // Add an attribute to disambiguate with the universalJar
            attribute(Attribute.of("net.neoforged.neoforge.includes-minecraft", Boolean), true)
        }
    }
    modDevBundle {
        canBeDeclared = false
        canBeResolved = false
        extendsFrom neoFormData
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, "data"))
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
        }
        outgoing {
            capability("net.neoforged:neoforge-moddev-bundle:${project.version}")
        }
        javaComponent.addVariantsFromConfiguration(it) {} // Publish it
    }
    modDevConfig {
        canBeDeclared = false
        canBeResolved = false
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, "data"))
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
        }
        outgoing {
            capability("net.neoforged:neoforge-moddev-config:${project.version}")
        }
        javaComponent.addVariantsFromConfiguration(it) {} // Publish it
    }
    installerJar {
        canBeDeclared = false
        canBeResolved = false
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EMBEDDED))
            // The installer targets JDK 8
            attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, 8)
        }
        outgoing {
            capability("net.neoforged:neoforge-installer:${project.version}")
        }
        // Publish it
        javaComponent.addVariantsFromConfiguration(it) {}
    }
    universalJar {
        canBeDeclared = false
        canBeResolved = false
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
            attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, java_version as Integer)
            attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements, LibraryElements.JAR))
        }
        // Publish it
        javaComponent.addVariantsFromConfiguration(it) {}
    }

    modDevApiElements {
        canBeDeclared = false
        canBeResolved = false
        extendsFrom libraries, moduleLibraries, userdevCompileOnly, neoFormDependencies
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_API))
        }
        outgoing {
            capability("net.neoforged:neoforge-dependencies:${project.version}")
        }
        javaComponent.addVariantsFromConfiguration(it) {}
    }
    modDevRuntimeElements {
        canBeDeclared = false
        canBeResolved = false
        extendsFrom libraries, moduleLibraries, neoFormDependencies
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
        }
        outgoing {
            capability("net.neoforged:neoforge-dependencies:${project.version}")
        }
        javaComponent.addVariantsFromConfiguration(it) {}
    }
    modDevModulePath {
        canBeDeclared = false
        canBeResolved = false
        extendsFrom moduleLibraries
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
        }
        outgoing {
            capability("net.neoforged:neoforge-moddev-module-path:${project.version}")
        }
        javaComponent.addVariantsFromConfiguration(it) {}
    }
    modDevTestFixtures {
        canBeDeclared = false
        canBeResolved = false
        extendsFrom userdevTestFixtures
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
        }
        outgoing {
            capability("net.neoforged:neoforge-moddev-test-fixtures:${project.version}")
        }
        javaComponent.addVariantsFromConfiguration(it) {}
    }
}

processResources {
    inputs.property("version", project.version)
    final version = project.version
    filesMatching("META-INF/neoforge.mods.toml") {
        expand([
                "global": [
                        "neoForgeVersion": version
                ]
        ])
    }
}

artifacts {
    modDevBundle(userdevJar) {
        setClassifier("userdev") // Legacy
    }
    modDevConfig(writeUserDevConfig.userDevConfig) {
        setClassifier("moddev-config")
    }
    universalJar(universalJar) {
        setClassifier("universal")
    }
    installerJar(installerJar) {
        setClassifier("installer")
    }
}

task downloadMultipleFiles(type: Download) {
    src([
            'https://maven.mohistmc.com/libraries/1.21.1/youer/libraries.zip'
    ])
    dest layout.buildDirectory.dir("libraries")
}

configurations {
    installer {
        // Don't pull all libraries, if we're missing something, add it to the installer list so the installer knows to download it.
        transitive = false
    }
}

def installerDeps = {
    installer "net.neoforged.installertools:installertools:${project.installertools_version}"
    installer "net.neoforged.installertools:binarypatcher:${project.installertools_version}:fatjar"
    installer "net.neoforged.installertools:jarsplitter:${project.installertools_version}"
    installer "net.neoforged.installertools:cli-utils:${project.installertools_version}"
    installer "net.neoforged:AutoRenamingTool:${project.art_version}:all"
    installer "de.siegmar:fastcsv:2.0.0"
}

dependencies installerDeps

task youerJar(type: Jar, dependsOn: [youerJar0, downloadMultipleFiles, project(':youerlauncher').tasks.jar]) {
    archiveClassifier = 'server'
    archiveExtension = 'jar'
    archiveBaseName = 'youer'
    archiveVersion = '1.21-' + gradleutils.gitInfo.abbreviatedId
    destinationDirectory = file('build/libs')

    manifest {
        attributes([
                'Premain-Class': 'com.mohistmc.launcher.youer.util.JarLoader',
                'Launcher-Agent-Class': 'com.mohistmc.launcher.youer.util.JarLoader',
                'Main-Class': 'com.mohistmc.launcher.youer.Main'
        ])
        attributes([
                'Specification-Title'   : 'Youer',
                'Specification-Vendor'  : 'MohistMC',
                'Implementation-Title'  : 'Youer',
                'Implementation-Version': archiveVersion,
                'Implementation-Vendor' : 'MohistMC'
        ] as LinkedHashMap, 'com/mohistmc/launcher/youer/')
    }

    //println(CLASS_PATH)

    from(zipTree(project(':youerlauncher').tasks.jar.outputs.getFiles().asPath.replace(".jar", "-all.jar"))) {
        into ""
    }
    from(zipTree(youerJar0.outputs.getFiles().asPath)) {
        into ""
    }

    from(zipTree(downloadMultipleFiles.outputs.getFiles().asPath)) {
        into 'META-INF/libraries'
    }

    configurations.libraries.resolvedConfiguration.resolvedArtifacts.collect {
        def moduleVersion = it.moduleVersion
        if (!moduleVersion.id.name.startsWith("asm")) {
            from(it.file) {
                into("META-INF/libraries/${moduleVersion.id.group.replace('.', '/')}/${moduleVersion.id.name}/${moduleVersion.id.version}/")
            }
        }
    }
    configurations.installer.resolvedConfiguration.resolvedArtifacts.collect {
        def moduleVersion = it.moduleVersion
        from(it.file) {
            into("META-INF/libraries/${moduleVersion.id.group.replace('.', '/')}/${moduleVersion.id.name}/${moduleVersion.id.version}/")
        }
    }

    configurations.moduleLibraries.resolvedConfiguration.resolvedArtifacts.collect {
        def moduleVersion = it.moduleVersion
        if (moduleVersion.id.name != "JarJarFileSystems") {
            from(it.file) {
                into("META-INF/libraries/${moduleVersion.id.group.replace('.', '/')}/${moduleVersion.id.name}/${moduleVersion.id.version}/")
            }
        }
    }

    from(rootProject.file('server_files/version.txt')) {
        filter(ReplaceTokens, tokens: [VERSION: gradleutils.gitInfo.abbreviatedId])
        rename { 'versions/youer.txt' }
    }
    from(rootProject.file('server_files/version.txt')) {
        filter(ReplaceTokens, tokens: [VERSION: minecraft_version])
        rename { 'versions/minecraft.txt' }
    }
    from(rootProject.file('server_files/version.txt')) {
        filter(ReplaceTokens, tokens: [VERSION: neoforge_version])
        rename { 'versions/neoforge.txt' }
    }
    from(rootProject.file('server_files/version.txt')) {
        filter(ReplaceTokens, tokens: [VERSION: minecraft_version + '-' + neoform_version])
        rename { 'versions/mcp.txt' }
    }
}

