pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenLocal()
        maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        maven { url = 'https://maven.mohistmc.com/' }
        maven { url = 'https://maven.neoforged.net/releases' }
    }
}

plugins {
    id 'net.neoforged.moddev.repositories' version "${moddevgradle_plugin_version}"
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

// This makes the version available to buildSrc
gradle.ext.moddevgradle_plugin_version = moddevgradle_plugin_version
gradle.ext.minecraft_version = minecraft_version
gradle.ext.diffpatch_version = diffpatch_version
gradle.ext.asm_version = asm_version

dependencyResolutionManagement {
    repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
    rulesMode = RulesMode.FAIL_ON_PROJECT_RULES
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        maven { url = 'https://maven.mohistmc.com/' }
        maven { url = "https://repo.papermc.io/repository/maven-public/" }
        maven { url = "https://jitpack.io" }
        maven { url = "https://maven.createmod.net" } // Create, Ponder, Flywheel
    }
}

if (rootProject.name.toLowerCase() == "youer") {
    // Solve name clashes between projects/neoforge and the rootProject for Eclipse.
    rootProject.name = "Youer-Root"
}

include ':base'
project(':base').projectDir = file('projects/base')
include ':youer'
project(':youer').projectDir = file('projects/youer')

include 'youerlauncher'

include ':coremods'
// Ensure a unique artifact id within JIJ that does not conflict with net.neoforged:coremods, which is
// another external dependency.
project(":coremods").name = "neoforge-coremods"

// Expose the repository base URLs to projects to be able to access them
// See this Gradle issue for tracking a more permanent solution:
// https://github.com/gradle/gradle/issues/27260
gradle.settingsEvaluated {
    gradle.extensions.add("repositoryBaseUrls", dependencyResolutionManagement.repositories.findAll { it instanceof MavenArtifactRepository }.collect { it.url }.toArray(URI[]::new))
}
